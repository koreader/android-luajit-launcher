apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.targetSdk

    defaultConfig {
        applicationId "org.koreader.einktest"
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        versionCode 10
        versionName "1.2"
    }

    sourceSets {
        main {
            manifest.srcFile('AndroidManifest.xml')
            java.srcDirs = ['src']
            res.srcDirs = ['res']
        }
    }

    variantFilter { variant ->
        if (variant.buildType.name == 'debug') {
            setIgnore(true)
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "einkTest.apk"
        }
    }

    lintOptions {
        checkReleaseBuilds true
        abortOnError false

        // disable on test applications
        disable 'GoogleAppIndexingWarning'
        disable 'MissingApplicationIcon'

        // reflection
        disable 'PrivateApi'

        // untranslatable string literals
        disable 'SetTextI18n'
    }
}

// copy EPD abstract classes from the main module before the build
task copyEPDFiles(type: Copy) {
    from '../../app/src/org/koreader/launcher/device/rockchip/RK30xxEPDController.java'
    from '../../app/src/org/koreader/launcher/device/rockchip/RK33xxEPDController.java'
    from '../../app/src/org/koreader/launcher/device/freescale/NTXEPDController.java'
    into 'src/org/koreader/test/eink/'
}

preBuild.dependsOn(copyEPDFiles)
