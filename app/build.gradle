apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.targetSdk

    defaultConfig {
        applicationId "org.koreader.launcher"

        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk

        versionCode versCode as Integer
        versionName versName
    }

    buildTypes {
        release {
            minifyEnabled false
            resValue "string", "app_name", projectName
            resValue "string", "app_flavor", projectFlavor
        }

        debug {
            minifyEnabled false
            debuggable true
            resValue "string", "app_name", projectName
            resValue "string", "app_flavor", projectFlavor
        }
    }

    sourceSets {
        main {
            manifest.srcFile('AndroidManifest.xml')
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = [ '../assets' ]
        }
    }

    flavorDimensions "abi"
    productFlavors {
        x86 {
            ndk { abiFilters "x86" }
            dimension = 'abi'
        }
        arm {
            ndk { abiFilters "armeabi-v7a" }
            dimension = 'abi'
        }
    }

    externalNativeBuild {
        ndkBuild {
            path file('../jni/Android.mk')
        }
    }

    lintOptions {
        checkReleaseBuilds true
        abortOnError false

        // reflection
        disable 'PrivateApi'

        // WRITE_SETTINGS permission
        disable 'ProtectedPermissions'

        // Some application flags were introduced on newer API levels and they
        // won't be used when targeting low level apis.
        // This applies to android:resizeableActivity and android:usesClearTextTraffic,
        // both available on API level 23 or higher.
        disable 'UnusedAttribute'
    }

    aaptOptions {
        cruncherEnabled = false
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "NativeActivity.apk"
        }
    }
}

dependencies {
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
}
