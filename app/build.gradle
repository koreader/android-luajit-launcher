apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "org.koreader.launcher"

        // Required when setting minSdkVersion to 20 or lower
        multiDexEnabled true

        minSdkVersion 14

        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 28

        versionCode versCode as Integer
        versionName versName
        manifestPlaceholders.APP_NAME = projectName
        buildConfigField 'String', 'APP_NAME', '"' + projectName + '"'
        buildConfigField "boolean", "IN_APP_UPDATES", "false"
        buildConfigField "boolean", "SUPPORTS_RUNTIME_CHANGES", "false"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
        }

        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            debuggable true
        }
    }

    sourceSets {
        main {
            assets.srcDirs = [ '../assets' ]
        }
    }

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true

        // Sets Java compatibility to Java 8
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions 'ABI', 'CHANNEL'
    productFlavors {
        x86 {
            ndk { abiFilters "x86" }
            dimension = 'ABI'
        }
        x86_64 {
            ndk { abiFilters "x86_64" }
            dimension = 'ABI'
            minSdkVersion 21
        }
        arm {
            ndk { abiFilters "armeabi-v7a" }
            dimension = 'ABI'
        }
        arm64 {
            ndk { abiFilters "arm64-v8a" }
            dimension = 'ABI'
            minSdkVersion 21
        }

        fdroid {
            dimension = 'CHANNEL'
        }
        rocks {
            dimension = 'CHANNEL'
            buildConfigField "boolean", "IN_APP_UPDATES", "true"
            buildConfigField "boolean", "SUPPORTS_RUNTIME_CHANGES", "true"
        }
    }

    externalNativeBuild {
        ndkBuild {
            path file('../jni/Android.mk')
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "NativeActivity.apk"
        }
    }
}

repositories {
    google()
    mavenCentral()
}

dependencies {
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$android_desugar_jdk"

    implementation "androidx.core:core-ktx:$androidx_core_version"
    implementation "androidx.appcompat:appcompat:$androidx_appcompat_version"
    implementation "androidx.legacy:legacy-support-v4:$androidx_supportv4_version"

    //noinspection GradleDependency
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_plugin_version"
    implementation "org.apache.commons:commons-compress:$commons_compress_version"

    // add Leak Canary on debug variants.
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"
}
