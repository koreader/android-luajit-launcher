apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion targetSdk

    defaultConfig {
        applicationId "org.koreader.launcher"
        minSdkVersion minSdk
        targetSdkVersion targetSdk
        versionCode versCode as Integer
        versionName versName
    }

    buildTypes {
        release {
            manifestPlaceholders.APP_NAME = projectName
            buildConfigField 'String', 'APP_NAME', '"' + projectName + '"'
            minifyEnabled true
            shrinkResources true
        }

        debug {
            manifestPlaceholders.APP_NAME = projectName + "-debug"
            buildConfigField 'String', 'APP_NAME', '"' + projectName + '-debug"'
            minifyEnabled false
            debuggable true
        }
    }

    sourceSets {
        main {
            manifest.srcFile('manifest/base.xml')
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = [ '../assets' ]
        }
        rocks {
            manifest.srcFile('manifest/rocks.xml')
            java.srcDirs = ['rocks']
        }
        fdroid {
            java.srcDirs = ['fdroid']
        }
    }

    flavorDimensions 'ABI', 'CHANNEL'
    productFlavors {
        x86 {
            ndk { abiFilters "x86" }
            dimension = 'ABI'
        }
        arm {
            ndk { abiFilters "armeabi-v7a" }
            dimension = 'ABI'
        }
        fdroid {
            dimension = 'CHANNEL'
        }
        rocks {
            dimension = 'CHANNEL'
        }
    }

    externalNativeBuild {
        ndkBuild {
            path file('../jni/Android.mk')
        }
    }

    lintOptions {
        checkReleaseBuilds true
        abortOnError false
        disable 'PrivateApi'
        disable 'ProtectedPermissions'
        disable 'UnusedAttribute'
        disable 'SetTextI18n'
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "NativeActivity.apk"
        }
    }
}

repositories {
    google()
    jcenter()
    mavenCentral()
}

dependencies {
    implementation "androidx.core:core-ktx:$androidx_core_version"
    implementation "androidx.appcompat:appcompat:$androidx_appcompat_version"
    implementation "androidx.legacy:legacy-support-v4:$androidx_supportv4_version"

    // blocked by the build system. New kotlin plugin requires a bump in gradle version,
    // which requires available toolchains for armeabi-v7a, arm64-v8a, x86 and x86_64
    //noinspection GradleDependency
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_plugin_version"

    // add Leak Canary on debug variants.
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"
}
