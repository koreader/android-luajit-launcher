apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    ndkPath ndkCustomPath
    compileSdkVersion rootProject.ext.compileSdk
    def baseVersionCode = versCode as Integer

    defaultConfig {
        applicationId "org.koreader.launcher"
        versionName versName
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk

        multiDexEnabled true
        manifestPlaceholders.APP_NAME = projectName

        buildConfigField "String", "APP_NAME", '"' + projectName + '"'
        buildConfigField "boolean", "IN_APP_UPDATES", "false"
        buildConfigField "boolean", "SUPPORTS_RUNTIME_CHANGES", "false"
    }

    sourceSets {
        main {
            assets.srcDirs = [ '../assets' ]
            jniLibs.srcDirs = [ '../libs' ]
        }
    }

    dependenciesInfo {
        includeInApk = false
    }

    externalNativeBuild {
        ndkBuild {
            path file('../jni/Android.mk')
        }
    }

    buildFeatures {
        viewBinding = true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            debuggable true
        }
    }

    flavorDimensions 'ABI', 'CHANNEL'
    productFlavors {
        arm {
            ndk { abiFilters "armeabi-v7a" }
            dimension = 'ABI'
            versionCode = baseVersionCode * 10 + 1
        }
        arm64 {
            ndk { abiFilters "arm64-v8a" }
            dimension = 'ABI'
            versionCode = baseVersionCode * 10 + 2
        }
        x86 {
            ndk { abiFilters "x86" }
            dimension = 'ABI'
            versionCode = baseVersionCode * 10 + 3
        }
        x86_64 {
            ndk { abiFilters "x86_64" }
            dimension = 'ABI'
            versionCode = baseVersionCode * 10 + 4
        }

        fdroid {
            dimension = 'CHANNEL'
            applicationIdSuffix = '.fdroid'
        }
        rocks {
            dimension = 'CHANNEL'
            buildConfigField "boolean", "IN_APP_UPDATES", "true"
            buildConfigField "boolean", "SUPPORTS_RUNTIME_CHANGES", "true"
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
    }

    packagingOptions {
        exclude '/kotlin/**.kotlin_builtins'
        exclude '/kotlin/**.kotlin_metadata'
        exclude '/META-INF/**.kotlin_module'
        exclude '/META-INF/**.version'
    }

    lintOptions {
        xmlReport false
        htmlReport false
        disable 'QueryAllPackagesPermission'
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "NativeActivity.apk"
        }
    }
}

repositories {
    google()
    mavenCentral()
}

dependencies {
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$android_desugar_jdk"

    //noinspection GradleDependency
    implementation "androidx.core:core-ktx:$androidx_core_version"
    //noinspection GradleDependency
    implementation "androidx.appcompat:appcompat:$androidx_appcompat_version"
    //noinspection GradleDependency
    implementation "androidx.legacy:legacy-support-v4:$androidx_supportv4_version"
    //noinspection GradleDependency
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_plugin_version"
    implementation "org.apache.commons:commons-compress:$commons_compress_version"

}
