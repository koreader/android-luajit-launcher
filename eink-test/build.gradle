apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.sdkVersion

    defaultConfig {
        applicationId "org.koreader.einktest"
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
    }

    sourceSets {
        main {
            manifest.srcFile('AndroidManifest.xml')
            java.srcDirs = ['src']
            res.srcDirs = ['res']
        }
    }

    flavorDimensions "abi"
    productFlavors {
        arm {
            ndk { abiFilters "armeabi-v7a" }
            dimension = 'abi'
        }
        x86 {
            ndk { abiFilters "x86" }
            dimension = 'abi'
        }
    }

    variantFilter { variant ->
        if (variant.buildType.name == 'debug') {
            setIgnore(true)
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "einkTest-${variant.getFlavorName()}.apk"
        }
    }

    lintOptions {
        checkReleaseBuilds true
        abortOnError false
        disable 'GoogleAppIndexingWarning'
        disable 'MissingApplicationIcon'
    }
}

// copy EPD abstract classes from the main module before the build
task copyEPDFiles(type: Copy) {
    from '../launcher/src/org/koreader/device/rockchip/RK30xxEPDController.java'
    from '../launcher/src/org/koreader/device/rockchip/RK33xxEPDController.java'
    from '../launcher/src/org/koreader/device/freescale/NTXEPDController.java'
    into 'src/org/koreader/test/eink/'
}

preBuild.dependsOn(copyEPDFiles)
